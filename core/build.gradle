apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// A properties file is located at projectFolder/keys.properties
def keysPropertiesFile = rootProject.file("keys.properties")
def keys = new Properties()
keys.load(new FileInputStream(keysPropertiesFile))

android {
    compileSdkVersion versions.compiledSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Safaricom API credentials
        buildConfigField("String", "safaricomConsumerKey", keys['SAFARICOM_CONSUMER_KEY'])
        buildConfigField("String", "safaricomConsumerSecret", keys['SAFARICOM_CONSUMER_SECRET'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:${versions.support}"

    // Firebase
    implementation "com.google.firebase:firebase-core:${versions.firebase.core}"
    implementation "com.google.firebase:firebase-auth:${versions.firebase.auth}"
    implementation "com.google.firebase:firebase-firestore:${versions.firebase.firestore}"

    // REST
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttpLogging}"

    // Rx
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rxkotlin}"

    // Other
    implementation "com.google.code.gson:gson:${versions.gson}"

    // Room
    implementation "android.arch.persistence.room:runtime:${versions.room}"
    implementation "android.arch.persistence.room:rxjava2:${versions.room}"
    kapt "android.arch.persistence.room:compiler:${versions.room}"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}