apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

def debugApiBaseUrl = "\"https://dev.justjava.store/\""
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(localPropertiesFile))

    // Ability to provide a different debug url
    def overrideUrl = localProperties.getProperty("debugApiBaseUrl")
    if (overrideUrl != null) {
        debugApiBaseUrl = overrideUrl
    }
}

android {

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "String", "API_BASE_URL", debugApiBaseUrl
        }
        release {
            minifyEnabled true
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField "String", "API_BASE_URL", "\"https://api.justjava.store/\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.70"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.5"

    implementation "androidx.appcompat:appcompat:1.1.0"

    // REST
    implementation "com.squareup.retrofit2:retrofit:2.6.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.4.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.0.0"

    // Other
    api "org.koin:koin-android:2.0.1"
    implementation "com.jakewharton.timber:timber:4.7.1"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.3.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-alpha01"
    testImplementation "io.mockk:mockk:1.9.2"
}